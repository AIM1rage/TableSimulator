class Table {
    //(x, y) - top left corner coordinates, tableSize - the tableSize of table
    field int x, y;
    field int tableSize;
    field int cellSize;
    field Array cells;
    field itn playerX, playerY;

    constructor Table new() {
        var int i, j;
        let x = 128;
        let y = 0;
        let tableSize = 256;
        let cellSize = 64;
        let cells = Array.new(16);
        let playerX = 3;
        let playerY = 3;
        let j = 0;
        while (j < 4) {
            let i = 0;
            while (i < 4) {
                let cells[j * 4 + i] = Cell.new(cellSize * i + x, cellSize * j + y, cellSize, j * 4 + i);
                let i = i + 1;
            }
            let j = j + 1;
        }
        return this;
    }

    method void print() {
        var int i, j;
        var Cell temp;
        do printFrame();
        let j = 0;
        while (j < 4) {
            let i = 0;
            while (i < 4) {
                let temp = cells[j * 4 + i];
                do temp.print();
                let i = i + 1;
            }
            let j = j + 1;
        }
        return;
    }

    method void printFrame() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + tableSize - 1, y + tableSize - 1);
        do Screen.setColor(false);
        do Screen.drawRectangle(x + 1, y + 1, x + tableSize - 2, y + tableSize - 2);
        return;
    }

    method void moveCells(Cell cell1, Cell cell2) {
        var int value1, value2;
        let value1 = cell1.getValue();
        let value2 = cell2.getValue();
        do cell1.setValue(value2);
        do cell2.setValue(value1);
        do print();
        return;
    }
}