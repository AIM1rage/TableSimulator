class Game {
    field Table table;
    field int actionsCount;

    constructor Game new() {
        return this;
    }

    method void run(int levelIndex) {
        var boolean finished;
        var char key;
        var Array levels, gameField;
        var Level level;
        var int n;
        var String name;
        var boolean exit;
        let levels = Levels.getLevels();
        let level = levels[levelIndex];
        let n = level.getN();
        let name = level.getName();
        let gameField = level.getGameField();
        let table = Table.new(n, gameField);
        let actionsCount = 20;
        do printTable(name);
        while (~finished) {
            let finished = table.check() | (actionsCount = 0);
            //up-arrow
            if (key = 131) {
                let actionsCount = actionsCount - table.makeMove(1);
            }
            //down-arrow
            if (key = 133) {
                let actionsCount = actionsCount - table.makeMove(2);
            }
            //right-arrow
            if (key = 132) {
                let actionsCount = actionsCount - table.makeMove(3);
            }
            //left-arrow
            if (key = 130) {
                let actionsCount = actionsCount - table.makeMove(4);
            }
            do Output.moveCursor(0, 61);
            do Output.printInt(actionsCount);
            do Output.printString(" ");
            let key = Keyboard.keyPressed();
        }
        if (actionsCount > 0) {
            do printFinishWindow(true);
        } else {
            do printFinishWindow(false);
        }
        return;
    }

    method void printTable(String name) {
        do Screen.clearScreen();
        do table.print();
        do Output.moveCursor(0, 0);
        do Output.printString("Level");
        do Output.moveCursor(1, 0);
        do Output.printString(name);
        do Output.moveCursor(0, 53);
        do Output.printString("Actions");
        return;
    }

    method void printFinishWindow(boolean isWinner) {
        do Sys.wait(100);
        do Screen.clearScreen();
        if (isWinner) {
            do Display.printShalost(104 * 32 + (232 / 16));
        } else {
            do Display.printZ(104 * 32 + (232 / 16));
        }
        do Sys.wait(5000);
        do Screen.clearScreen();
        return;
    }
}