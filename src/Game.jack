class Game {
    field Table table;
    field int actionsCount;

    constructor Game new() {
        return this;
    }

    method void run(Level level) {
        var boolean finished;
        var char key;
        var Array gameField;
        var int n;
        var String name;
        let n = level.getN();
        let name = level.getName();
        let gameField = level.getGameField();
        let table = Table.new(n, gameField);
        let actionsCount = 200;
        do printTable(name);
        while (~finished) {
            do printActionsCounter();
            while (~(key = 0)) { let key = Keyboard.keyPressed(); }
            while (key = 0)    { let key = Keyboard.keyPressed(); }
            //up-arrow
            if (key = 131) {
                let actionsCount = actionsCount - table.makeMove(1);
            }
            //down-arrow
            if (key = 133) {
                let actionsCount = actionsCount - table.makeMove(2);
            }
            //right-arrow
            if (key = 132) {
                let actionsCount = actionsCount - table.makeMove(3);
            }
            //left-arrow
            if (key = 130) {
                let actionsCount = actionsCount - table.makeMove(4);
            }
            let finished = table.check() | (actionsCount = 0);
            let key = Keyboard.keyPressed();
        }
        if (actionsCount > 0) {
            do printFinishWindow(true);
        } else {
            do printFinishWindow(false);
        }
        return;
    }

    method void printTable(String name) {
        do Screen.clearScreen();
        do table.print();
        do Output.moveCursor(0, 0);
        do Output.printString("Level");
        do Output.moveCursor(1, 0);
        do Output.printString(name);
        do Output.moveCursor(0, 51);
        do Output.printString("Actions");
        return;
    }

    method void printFinishWindow(boolean isWinner) {
        var int offset;
        do Sys.wait(100);
        do Screen.clearScreen();
        let offset = 104 * 32 + (232 / 16);
        if (isWinner) {
            do Display.printShalost(offset);
        } else {
            do Display.printNeudacha(offset);
        }
        do Sys.wait(5000);
        do Screen.clearScreen();
        return;
    }

    method void printActionsCounter() {
        do Output.moveCursor(0, 59);
        do Output.printInt(actionsCount);
        do Output.printString("  ");
        return;
    }

    method void dispose() {
        do table.dispose();
        do Memory.deAlloc(this);
        return;
    }
}